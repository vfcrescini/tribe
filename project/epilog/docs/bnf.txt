
BNF

Vino Crescini  <jcrescin@cit.uws.edu.au>

Started 2002 Jul 08
Updated 2002 Aug 23


<start>                 : <program>

<program>               : <init> <body> <destroy>

<init>                  :

<destroy>               :

<body>                  : <first-part> | <last-part> |

<first-part>            : <ident-stmt-list> | 
                          <ident-stmt-list> <last-part>

<last-part>             : <other-stmt-list>

<ident-stmt-list>       : <ident-stmt> | <ident-stmt> <ident-stmt-list>
                        
<other-stmt-list>       : <other-stmt> | <other-stmt> <other-stmt-list>
                        
<other-stmt>            : <initial-stmt> | <trans-stmt> | <policy-stmt>

<ident-stmt>            : <ident> <ident-declaration> <semicolon>

<ident-declaration>     : <sub-ident-decl> | <obj-ident-decl> | acc-ident-decl> |
                          <sub-grp-ident-decl> | <obj-grp-ident-decl> |
                          <acc-grp-ident-decl>

<sub-ident-decl>        : <sub-type> <sub-ident-list>

<acc-ident-decl>        : <acc-type> <acc-ident-list>

<obj-ident-decl>        : <obj-type> <obj-ident-list>

<sub-grp-ident-decl>    : <sub-grp-type> <sub-grp-ident-list>

<acc-grp-ident-decl>    : <acc-grp-type> <acc-grp-ident-list>

<obj-grp-ident-decl>    : <obj-grp-type> <obj-grp-ident-list>

<sub-ident-list>        : <identifier> | <identifier> <comma> <sub-ident-list>

<obj-ident-list>        : <identifier> | <identifier> <comma> <obj-ident-list>

<acc-ident-list>        : <identifier> | <identifier> <comma> <acc-ident-list>

<sub-grp-ident-list>    : <identifier> | <identifier> <comma> <sub-grp-ident-list>

<obj-grp-ident-list>    : <identifier> | <identifier> <comma> <obj-grp-ident-list>

<acc-grp-ident-list>    : <identifier> | <identifier> <comma> <acc-grp-ident-list>

<initial-stmt>          : <initially> <ground-exp> <semicolon>
                        
<trans-stmt>            : <trans> <identifier> <trans-var-def>
                          <causes> <comp-exp> <if> <comp-exp> <semicolon>
                        
<trans-var-def>         : <open-parent> <close-parent> |
                          <open-parent> <trans-var-list> <close-parent> 
                        
<trans-var-list>        : <identifier> | <identifier> <comma>
                          <trans-var-list>
                        
<policy-stmt>           : <is-clause> <after-clause> <semicolon> |
                          <is-clause> <semicolon>
                        
<is-clause>             : <is> <ground-exp>
                        
<after-clause>          : <after> <trans-ref-list>
                        
<trans-ref-list>        : <trans-ref-def> | <trans-ref-def> <comma> 
                          <trans-ref-list>
                        
<trans-ref-def>         : <identifier> <open-parent>
                          <trans-ref-ident-list> <close-parent>
                        
<trans-ref-ident-list>  : <identifier> | <identifier> <comma>
                          <trans-ref-ident-list>

<ground-exp>            : <ground-boolean-atom> <logical-op> <ground-exp> |
                          <ground-boolean-atom>
                        
<ground-boolean-atom>   : <not> <ground-atom-exp> | <ground-atom-exp>
                        
<ground-atom-exp>       : <ground-holds-atom> | <ground-subst-atom> | 
                          <ground-memb-atom> | <logical-atom>
                        
<ground-holds-atom>     : <holds> <open-parent> <identfier> <comma>
                          <identifier> <comma> <identifier> <close-parent>
 
<ground-subst-atom>     : <subset> <open-parent> <identifier>
                          <comma> <identifier> <close-parent>
                        
<ground-memb-atom>      : <member> <open-parent> <identifier>
                          <comma> <identifier> <close-parent>

<comp-exp>              : <comp-boolean-atom> <logical-op> <comp-exp> |
                          <comp-boolean-atom>

<comp-boolean-atom>     : <not> <comp-atom-exp> | <comp-atom-exp>

<comp-atom-exp>         : <comp-holds-atom> | <comp-subst-atom> |
                          <comp-memb-atom> | <logical-atom>

<comp-holds-atom>       : <holds> <open-parent> <identfier> <comma>
                          <identifier> <comma> <identifier> <close-parent>
 
<comp-subst-atom>       : <subset> <open-parent> <identifier>
                          <comma> <identifier> <close-parent>
                        
<comp-memb-atom>        : <member> <open-parent> <identifier>
                          <comma> <identifier> <close-parent>

<logical-atom>          : <true> | <false>
                        
<logical-op>            : <and>
                        
<identifier>            : [a-zA-Z][a-zA-Z0-9_]{0,127}

<true>                  : true
                        
<false>                 : false
                        
<and>                   : &&
                        
<not>                   : !
                        
<holds>                 : holds
                        
<member>                : memb
                        
<subset>                : subst
                        
<transform>             : trans

<initially>             : initially
                        
<causes>                : causes
                        
<if>                    : if
                        
<is>                    : is
                        
<after>                 : after

<ident>                 : ident

<sub-type>              : sub

<obj-type>              : obj

<acc-type>              : acc

<sub-grp-type>          : sub-grp

<obj-grp-type>          : obj-grp

<acc-grp-type>          : acc-grp
                        
<underscore>            : _

<open-parent>           : (

<close-parent>          : )

<comma>                 : ,
                        
<semicolon>             : ;
