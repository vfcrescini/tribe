Vino Crescini  <jcrescin@cit.uws.edu.au>
2002 July 08

BNF

<alpha>                 : <small-caps> | <large-caps>
                        
<small-caps>            : a | b | c | d | e | f | g | h | i | j | k | l | m | n |
                          o | p | q | r | s | t | u | v | w | x | y | z
                        
<large-caps>            : A | B | C | D | E | F | G | H | I | J | K | L | M | N |
                          O | P | Q | R | S | T | U | V | W | X | Y | Z
                        
<digit>                 : 0 | 1 | 2 | 3 | 4 | 5 | 6 | 7 | 8 | 9
                        
<alpha-num>             : <alpha> | <digit>
                        
<alpha-num-uscr>        : <alpha-num> | <underscore>
                        
<alpha-num-uscr-str>    : <alpha-num-uscr> | <alpha-num-uscr> <alpha-num-uscr-str>
                        
<s-subject-ident>       : s | s <alpha-num-uscr-str>
                        
<s-access-ident>        : a | a <alpha-num-uscr-str>
                        
<s-object-ident >       : o | o <alpha-num-uscr-str>
                        
<g-subject-ident>       : S | S <alpha-num-uscr-str>
                        
<g-access-ident>        : A | A <alpha-num-uscr-str>
                        
<g-object-ident >       : O | O <alpha-num-uscr-str>
                        
<ground-sub-ident>      : <s-subject-ident> | <g-subject-ident>
                        
<ground-acc-ident>      : <s-access-ident> | <g-access-ident>
                        
<ground-obj-ident>      : <s-object-ident> | <g-object-ident>
                        
<var-identifier>        : v | V | v <alpha-num-uscr-str> | 
                          V <alpha-num-usrc-str>
                        
<trans-identifier>      : t | T | t <alpha-num-uscr-str> | 
                          T <alpha-num-usrc-str>
                        
<sub-acc-obj-ident>     : <ground-sub-ident> |
                          <ground-acc-ident> |
                          <ground-obj-ident>
                        
<comp-sub-ident>        : <s-subject-ident> | <g-subject-ident> |
                          <var-identifier>
                        
<comp-acc-ident>        : <s-access-ident> | <g-access-ident>
                          <var-identifier>
                        
<comp-obj-ident>        : <s-object-ident> | <g-object-ident>
                          <var-identifier>

<comp-single-var-ident> : <s-subject-ident> | <s-access-ident> |
                          <s-object-ident> | <var-identifier>

<comp-group-var-ident>  : <g-subject-ident> | <g-access-ident> |
                          <g-object-ident> | <var-identifier>

<true>                  : true
                        
<false>                 : false
                        
<and>                   : &&
                        
<not>                   : !
                        
<holds>                 : holds
                        
<member>                : memb
                        
<subset>                : subst
                        
<transform>             : trans
                        
<initially>             : initially
                        
<causes>                : causes
                        
<if>                    : if
                        
<is>                    : is
                        
<after>                 : after
                        
<underscore>            : _

<open-parent>           : (

<close-parent>          : )

<comma>                 : ,
                        
<semicolon>             : ;
                        
<logical-const>         : <true> | <false>
                        
<logical-op>            : <and>
                        
<comp-exp>              : <comp-boolean-exp> <logical-op> <comp-exp> |
                          <comp-boolean-exp>
                        
<comp-boolean-exp>      : <not> <comp-atom> | <comp-atom>
                        
<comp-atom>             : <comp-holds> | <comp-subst> | <comp-memb> | 
                          <logical-const>
                        
<comp-holds>            : <holds> <open-parent> <comp-sub-ident> <comma>
                          <comp-acc-ident> <comma> <comp-obj-ident>
                          <close-parent>
                        
<comp-subst>            : <subset> <open-parent> <comp-grp-var-ident>
                          <comma> <comp-group-var-ident> <close-parent>
                        
<comp-memb>             : <member> <open-parent> <comp-single-var-ident>
                          <comma> <comp-group-var-ident> <close-parent>
                        
<ground-exp>            : <ground-boolean-exp> <logical-op> <ground-exp> |
                          <ground-boolean-exp>
                        
<ground-boolean-exp>    : <not> <ground-atom> | <ground-atom>
                        
<ground-atom>           : <ground-holds> | <ground-subst> | ground-memb> | 
                          <logical-const>
                        
<ground-holds>          : <holds> <open-parent> <ground-sub-ident> <comma>
                          <ground-acc-ident> <comma> <ground-obj-ident>
                          <close-parent>
                        
<ground-subst>          : <ground-subject-subst> | <ground-access-subst> |
                          <ground-object-subst>
                        
<ground-memb>           : <ground-subject-memb> | <ground-access-memb> |
                          <ground-object-memb>
                        
<ground-subject-subst>  : <subset> <open-parent> <g-subject-ident>
                          <comma> <g-subject-ident> <close-parent>
                        
<ground-object-subst>   : <subset> <open-parent> <g-object-ident>
                          <comma> <g-object-ident> <close-parent>
                        
<ground-access-subst>   : <subset> <open-parent> <g-access-ident>
                          <comma> <g-access-ident> <close-parent>
                        
<ground-subject-memb>   : <member> <open-parent> <s-subject-ident>
                          <comma> <g-subject-ident> <close-parent>
                        
<ground-access-memb>    : <member> <open-parent> <s-access-ident>
                          <comma> <g-access-ident> <close-parent>
                        
<ground-object-memb>    : <member> <open-parent> <s-object-ident>
                          <comma> <g-object-ident> <close-parent>
                        
<initial-stmt>          : <initially> <ground-exp> <semicolon>
                        
<trans-stmt>            : <trans> <trans-identifier> <trans-var-def>
                          <causes> <ground-exp> <if> <ground-exp> <semicolon>
                        
<trans-var-def>         : <open-parent> <close-parent> |
                          <open-parent> <trans-var-list> <close-parent> 
                        
<trans-var-list>        : <var-identifier> | <var-identifier> <comma>
                          <trans-var-list>
                        
<policy-stmt>           : <is-clause> <after-clause> <semicolon> |
                          <is-clause> <semicolon>
                        
<is-clause>             : <is> <ground-exp>
                        
<after-clause>          : <after> <trans-ref-list>
                        
<trans-ref-list>        : <trans-ref-def> | <trans-ref-def> <comma> 
                          <trans-ref-list>
                        
<trans-ref-def>         : <trans-identifier> <open-parent>
                          <sub-acc-obj-list> <close-parent>
                        
<sub-acc-obj-list>      : <sub-acc-obj-ident> | <sub-acc-obj-ident> <comma>
                          <sub-acc-obj-list>
                        
<program>               : <statement-list>
                        
<statement-list>        : <statement> | <statement> <statement-list>
                        
<statement>             : <initial-stmt> | <trans-stmt> | <policy-stmt>
